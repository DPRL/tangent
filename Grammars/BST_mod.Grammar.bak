%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% BST.Grammar
%	- Main grammar for BST processing.
%
% Copyright (C) 2000,2002,2004 Richard Zanibbi
%
% This program is free software; you can redistribute it and/or
% modify it under the terms of the GNU General Public License
% as published by the Free Software Foundation; either version 2
% of the License, or (at your option) any later version.
%
% This program is distributed in the hope that it will be useful,
% but WITHOUT ANY WARRANTY; without even the implied warranty of
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
% GNU General Public License for more details.
%
% You should have received a copy of the GNU General Public License
% along with this program; if not, write to the Free Software
% Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% Revision History:
% v. 1.0.0 July 6, 2000
% v. 1.0.1 Mar. 2, 2001
%	- adding optional FFES integer identifier
%	  to allow faster morph performance in
%	  FFES.
% v 1.0.2 March 14, 2001
%	- had to add [TAB] after bracket symbols..
%	  for some reason these were not being
%         separated from the bounding boxes in 
%         the output.
% v 1.0.3 March 15, 2001
%	- split up binop class into sub-types
%	  based on formatting conventions.
% v 1.1 January 31, 2002
%	- moved symbol classes into "Symbol_Classes.Grammar"
%	  to move towards a more flexible system, where
%	  different symbol classes can be loaded up.
% v 1.2 April 25, 2004
%	- adding latex format to [program] types, definitions at bottom of file;
%     hard-coded size for now.
%   - altered [symbol] to include [id] type, so that symbols can
%     be unquoted in the output.
%	- added tokens for symbols that must have an escape character in LaTeX
%     math mode.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
tokens
	under_score "_"
	hat "^"
	letter "\a" % Upper or lower case.
	digit "\d"

	texCommand	"\\[\a\\]+"  % changed from identifier; don't allow _
	citLabel 	"[\A\b]+[-]\b+([-]\b+)*"
	UW_III_expressionId "MT[\i-]+"
	
	% April 29, 2004: for characters that require escape sequences in LaTeX.
	ampersand_symbol "\\&"
	pound_symbol "\\\#"
	dollar_symbol "\\$"
	tex_open_bracket "\\{"
	tex_close_bracket "\\}"

	% RZ: June 13, 2011; added to make sure single quotes are not interpreted
	%     as string literals.
	single_quote "'"
		| "`"

	% RZ: July 11, 2011: Removing comments from tex2html
	tex2html_comment_mark "<tex2html_comment_mark>\d+"
end tokens

compounds
	':: 'REL 'ARG 'FN '\!
end compounds

% RZ: June 1, 2011 Addition
keys
	':: '{ '} [under_score] [hat]
end keys

define rlabel
	':: 'ARG [number] [NL]
end define

define mynumber
	[SPOFF] [repeat number+] [SPON]
end define

%
% Comment Definitions (used here to eliminate headers)
%
comments
	% Need to be careful not to use a sequence that can match
	% a symbol label!
	<-- -->
	//
	/* */	
end comments

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%			       GRAMMAR
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
define program
	% FFES or UW_III list formats.
		[opt ffesHeader] [baselineStructureTree]
	|	[repeat UW_III_Expression]
	|	[bst_file]
	|	[latex_file]
end define

define ffesHeader
		'Number 'of 'Symbols: [integernumber] [NL]
end define

define UW_III_Expression
		[NL][expressionLabel] [IN]
			[baselineStructureTree] [EX]	
	|	[NL][NL]'\bigskip [NL][NL]'% [expressionLabel] [NL]
	'\begin{displaymath} [IN]
			[baselineStructureTree] [EX] [NL]'\end{displaymath}
end define

define expressionLabel
		[UW_III_expressionId] ', [number] 'symbols, 'BB '( [number] [number] ', 
			[number] [number] ')
end define

% DRACULAE .bst files; want to be able to re-use these...
define bst_file
		[repeat labelled_bst]
end define

define labelled_bst
		[NL] [romanNumeral]'. [treeLabel] 
		[baselineStructureTree] [NL]
end define

% This is only up to ten, but this is all we should need.
define romanNumeral
		'I | 'II | 'III | 'IV | 'V | 'VI | 'VII | 'VIII
	|	'IX | 'X
end define

define treeLabel
		Baseline Structure Tree 
	|	Token Lexed BST 
	|	Relation Lexed BST 
end define


define baselineStructureTree
		[repeat symbol_node]
end define

define symbol_node
		[symbol] [repeat relation_node] [NL] %[attr 'VISITED] [NL]
	|	'{ [NL][IN]
			[baselineStructureTree]
		 [EX] '} [NL] [repeat relation_node]  % Debug
end define

define symbol
		[symbol_id_attributes] [IN] [bounding_box] [opt bounding_box]
		[opt centroid] [opt FFES_id] [EX]
    |   "DECIMAL_NUMBER" [IN] [stringlit] [IN] [bounding_box] [EX] [EX]
    |   [stringlit] [opt bounding_box]
	|	"SYMBOL" [symbol_id_attributes]
	% April 26, 2005 (for unquoting symbols)
	% RZ: Removed, June 1, 2011
	%|	[id]
	|	[ampersand_symbol] | [pound_symbol] %| [dollar_symbol]
end define

define FFES_id
	% This is here to allow FFES to quickly process DRACULAE output.
		'FFES_id: [number]
end define

define symbol_id_attributes
		[limit_symbol]
   |	[nonscripted_symbol]
   |	[root_symbol]
   |    [regular_symbol]
   % Note hack to accept accented symbols from UW_III dataset.
   % Apparently the primitive symbols weren't recorded in the
   % ground truth (!).
   %|	[texCommand]{[texCommand]}

   % RZ: Modified, June 1, 2011
   %|	[texCommand]{[id]}
   |	[texCommand]
   |	'\!
end define

define coordinate_pair
	'( [integernumber] ',  [integernumber] ')
end define

define bounding_box
 	'< [coordinate_pair] ', [coordinate_pair] '>
end define

define centroid
	[coordinate_pair]
end define

% RZ: Dirty Hack, June 2, 2011
define isymbol
		[symbol]
	|   ':: 'ARG [number] [NL] [IN] [symbol] [EX] [NL] '::
end define

%define ssymbol
%		[digit]
%	|	[letter]
%end define

define relation_node
		[NL] [IN] 
		[spatial_relation] '{ [NL] [IN] [baselineStructureTree] [EX] '} [EX]
    |   '{ [NL] [IN] [baselineStructureTree] [EX] '} 

	% RZ: June 2011 additions
	|	[NL] [spatial_relation] [NL] [rlabel] [IN] [baselineStructureTree] [EX] '::
	|	[NL] [rlabel] [IN] [baselineStructureTree] [EX] '::
	%|	[NL] [rlabel] [IN] [isymbol] % Addition for bug ({{\mathbb{R}}^2)
	|	[NL] [spatial_relation] [NL] [isymbol]
end define

define spatial_relation
		[relation_type]
	|   ':: 'REL [relation_type]
end define

define relation_type
		'ABOVE
     |	'BELOW
     |	'SUPER
     |  'SUBSC
	 
     |	'TLEFT
     |  'NEW_TLEFT
     |  'OLD_TLEFT
	 
     |	'BLEFT
     |  'NEW_BLEFT
     |  'OLD_BLEFT
	 
     |	'CONTAINS

     |  'ULIMIT
     |  'LLIMIT
	
	  % For scope of limit expressions (perhaps this shouldn't be here...)
	 |	'SCOPE
	 
     % TeX additions
	 |	 [under_score]
	 |	[hat]
     %|  '^
     %|  '_
     %|  [texCommand]
end define

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% (SIMPLE) LATEX SUB-GRAMMAR
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
define latex_file
	[latex_header] 
	%[TeX_BST]
	[baselineStructureTree]
	[latex_footer] [NL]
end define

define latex_doc
	[SPOFF]
	'\documentclass '[ '12pt ', 'letter '] '{ 'article '} [NL]
	[SPON]
end define

define tex_font_size
		'Huge
	|	'Large
	|	'large
end define

% NOTE: Font sizes must agree for \begin and \end.
define latex_header
	[latex_doc]
	 [SPOFF]
	 '\usepackage{ 'amssymb '} [NL] [NL]
	 '\begin{ 'document '}	[NL]
	 '\pagestyle{ 'empty '}	[NL]
	 '\begin{ [tex_font_size] '}		[NL] [NL]
	 '$ [NL]
	 [IN] [SPON]
end define

define latex_footer
	[EX] [SPOFF] 
	 '$				[NL] [NL]
     '\end{ [tex_font_size] '}		[NL]
	 '\end{document}	[NL]
	 [SPON]
end define

